---
title: "CPANモジュールを利用する"
---

Perlの文法を説明する前に、Perlの実践的な話を紹介します。ここにあるコードを理解できるようになることが、本書のゴールの一つです。

Perlは、awk、sedとC言語の隙間を埋めるように設計されました。UNIXのファイルシステムやデーターベースなど異なるソフトウェアコンポーネントやシステムを接着する「Glue言語」とも呼ばれます。例えば、標準入力をフィルタリングして標準出力するのは大得意で、次のコードはTSVをmarkdownのテーブルに変換しています。

```perl:to_markdown_table.pl
#!perl -g
use v5.42;
use utf8;
use open ':std', ':encoding(UTF-8)';
use List::Util 'max';

sub format_row($row) {
    '| ' . join(' | ', map { sprintf("%-*s", $widths[$_], $row->[$_] // '') } 0..$#widths) . ' |'
}

# TSVを読み込み
my @rows = map { [split /\t/] } split /\n/, <>;

my @widths;
for my $row (@rows) {
    for my ($i, $col) (indexed $row->@*) {
        $widths[$i] = max($widths[$i] // 0, length($col// ''));
    }
}

my ($header, @data) = @rows;
say format_row($header);
say '|' . join('|', map { '-' x ($_ + 2) } @widths) . '|';
say format_row($_) for @data;
```

```shell
% cat input.tsv
id	en	ja
1	apple	りんご
2	banana	バナナ
3	cherry	さくらんぼ

% cat input.tsv | ./to_markdown_table.pl
| id | en     | ja    |
|----|--------|-------|
| 1  | apple  | りんご   |
| 2  | banana | バナナ   |
| 3  | cherry | さくらんぼ |
```

接着の要が、CPANモジュールです。前節で、CPANモジュールとはPerlで書かれた再利用可能なパッケージとしてまとめられたコードと紹介しました。豊富にCPANモジュールがあるおかげで、仕事を簡単に片付けられます。例えば、上に書いたコードは、Text::MarkdownTable モジュールを利用してより簡潔に書けます。この規模の要件であればどちらでも良いと思いますが、複雑な要件になれば、意味を理解しやすくなって悪いことはないでしょう。

```perl:to_markdown_table_fixed.pl
#!perl -g
use v5.42;
use utf8;
use open ':std', ':encoding(UTF-8)';
use Text::MarkdownTable;

my ($header, @rows) = map { [split /\t/] } split /\n/, <>;

my $table = Text::MarkdownTable->new();
for my $row (@rows) {
    $table->add({ map { $header->[$_] => $row->[$_] // '' } 0 .. $#$header });
}
$table->done;
```

