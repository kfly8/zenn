---
title: "CPANモジュールを利用する"
---

Perlは、awk、sedとC言語の隙間を埋めるように設計されました。
UNIXのファイルシステムやデーターベースなど異なるソフトウェアコンポーネントやシステムを接着する「Glue言語」とも呼ばれます。
例えば、標準入力をフィルタリングして標準出力なんてお手の物で、次のコードはTSVをmarkdownのテーブルに変換しています。

```perl:to_markdown_table.pl
#!perl -g
use v5.42;
use utf8;
use open ':std', ':encoding(UTF-8)';
use List::Util 'max';

my @rows = map { [split /\t/] } split /\n/, <>;

my @widths;
for my $row (@rows) {
    for my ($i, $col) (indexed $row->@*) {
        $widths[$i] = max($widths[$i] // 0, length($col// ''));
    }
}

sub format_row($row) {
    '| ' . join(' | ', map { sprintf("%-*s", $widths[$_], $row->[$_] // '') } 0..$#widths) . ' |'
}

my ($header, @data) = @rows;
say format_row($header);
say '|' . join('|', map { '-' x ($_ + 2) } @widths) . '|';
say format_row($_) for @data;
```

```shell
% cat input.tsv
id	en	ja
1	apple	りんご
2	banana	バナナ
3	cherry	さくらんぼ

% cat input.tsv | ./to_markdown_table.pl
| id | en     | ja    |
|----|--------|-------|
| 1  | apple  | りんご   |
| 2  | banana | バナナ   |
| 3  | cherry | さくらんぼ |
```

接着の要が、CPANモジュールです。前節で、CPANモジュールとはPerlで書かれた再利用可能なパッケージとしてまとめられたコードと紹介しました。
例えば、上に書いたコードは、Text::MarkdownTable モジュールを利用すれば簡単に片付けられます。

```perl:to_markdown_table_fixed.pl
#!perl -g
use v5.42;
use utf8;
use open ':std', ':encoding(UTF-8)';
use Text::MarkdownTable;

my ($header, @rows) = map { [split /\t/] } split /\n/, <>;

my $table = Text::MarkdownTable->new();
for my $row (@rows) {
    $table->add({ map { $header->[$_] => $row->[$_] // '' } 0 .. $#$header });
}
$table->done;
```

複雑な仕事が降ってきても、きっとCPANモジュールを組み合わせ、繋ぎ合わせることで、仕事を片付けられるはずです。

