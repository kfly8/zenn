---
title: "定数"
---

## `constant`モジュールで定数定義

Perlで定数を定義したい場合、`constant`モジュールを利用します。

```perl
use constant PI => 4 * atan2(1, 1);
say PI * 2 * 2; # 12.5663706143592
```

この定数の実態は、ただの関数です。次のように関数定義しているのと変わりません。

```perl
sub PI :prototype() { 4 * atan2(1, 1) }
```

この`:prototype()`は、関数PIは引数を取らないことを宣言しています。

## 定数の畳み込み

定数を利用するとPerlのコンパイラが最適化をして定数の畳み込みを行います。例えば、次のコードでは、ASSERTが有効な時だけ、`$message`の値のチェックを行い、そうでない時は何もしません。

```perl
use constant ASSERT => $ENV{ASSERT} // 0;
use Carp qw(croak);

sub hello($message) {
    # ASSERTが有効な時だけ、文字列かどうか確認する。文字列かどうかの判定は、Types::StandardのStrなどユーティリティを利用すると良い
    ASSERT && do { croak '$message must be string' if !defined $message || reftype $message };
    return "Hello, $message!";
}

hello('World'); # OK
hello({});      # Error
hello(undef);   # Error
```

このパターンを利用すれば、引数のバリデーションコストをテストや開発中だけに限定できるので重宝します。

