---
title: "Perlの今"
---

### Perlの今

2022年、Perl v5.36のリリース以降、Perl本体の開発がこれまでにないほど活発です。組み込みのclass構文など言語機能の拡張が増えています。また、初学者を惑わす機能を予防する仕組みも導入され、プログラミング言語の現代的なプラクティスを取り入れる進化をしています。本書ではこの進化をできるだけお伝えしたいと思います。


TODO: 画像にしたい

#### BEFORE

```perl
package Cache::LRU;
use strict;
use warnings;

use Hash::Ordered;

sub new {
    my ($class, %args) = @_;
    my $self = {
        max_size => $args{max_size} // 20,
        _cache   => Hash::Ordered->new(),
    };
    return bless $self, $class;
}

sub set {
    my ($self, $key, $value) = @_;
    if ($self->{_cache}->exists($key)) {
        $self->{_cache}->delete($key);
    } elsif ($self->{_cache}->keys >= $self->{max_size}) {
        $self->{_cache}->shift;
    }
    $self->{_cache}->set($key, $value);
}

sub get {
    my ($self, $key) = @_;
    $self->{_cache}->get($key);
}

1;

my $cache = Cache::LRU->new(max_size => 3);

use feature qw(say);
$cache->set('a', 2);

say $cache->get('a'); # 1
```

#### AFTER

```perl
use v5.42;
use experimental qw(class);

use Hash::Ordered;

class Cache::LRU {
    field $max_size :param = 20;
    field $cache = Hash::Ordered->new();

    method set($key, $value) {
        if ($cache->exists($key)) {
            $cache->delete($key);
        } elsif ($cache->keys >= $max_size) {
            $cache->shift;
        }
        $cache->set($key, $value);
    }

    method get($key) {
        $cache->get($key);
    }
}

my $cache = Cache::LRU->new(max_size => 3);

$cache->set(a => 2);
say $cache->get('a'); # 2
```

この変化の背景の一つとして、Perlの文法拡張がPerl本体を変更せずとも可能になっていることが挙げられます。具体的には、PEVANS氏らによって、2010年(v5.12)のタイミングで、コンパイルフェーズにキーワードをフックしてASTを書き換えるkeyword pluginが導入され、Perlのモジュールで新機能の検証を行い、その後、Perl本体ん取り込まれるという流れが確立されました。
例えば、組み込みのclass構文は、[Corinna](https://github.com/Perl-Apollo/Corinna)でOvid氏によってデザインされ、PEVANS氏によって[Object::Pad](https://metacpan.org/pod/Object::Pad)が実装、検証され、その後、Perl本体に取り込まれました。

PEVANS氏は、London Perl Workshop 2016にて、こう語ったそうです。

> Perlをもっとよくしていくには、公開されている機能をギリギリまで使い倒して、できる人たちに「そんな無茶なやり方よりもっといい方法がある」と言わせるといい
> -- https://charsbar.hatenadiary.org/entry/20161217/1481954722 より引用

実際、拙作の文法拡張モジュール [Syntax::Keyword::Assert](https://metacpan.org/pod/Syntax::Keyword::Assert) を書いた際に、PEVANS氏からレビューをもらえたので、これは本当のことですね😄
