---
title: "関数"
---

## 関数の定義

Perlの関数、あるいはサブルーチンは、次のように`sub`キーワードで定義できます。

```perl
use v5.42;

sub add($x, $y) {
  return $x + $y;
}

say add(42, 13); # => 55
```

## 複数の返り値

関数の返り値を複数にしたい場合、リストや配列を利用します。

```perl
sub swap_list($x, $y) {
  return ($y, $x)
}

my ($x, $y) = swap_list('Hello','World');
$x; 'World';
$y; 'Hello';

sub swap_array($x, $y) {
  my @tmp = ($y, $x);
  return @tmp;
}

my ($x, $y) = swap_array('Hello','World');
$x; 'World';
$y; 'Hello';
```

ただし、間違えて、スカラーで受け取るプログラムを書いた場合、意図しない結果になります。

```perl
my $tmp = swap_list('Hello', 'World');
$tmp; # 'Hello' # swap後のリストの最後の要素が返るため、'Hello'が返る

my $tmp = swap_array('Hello', 'World');
$tmp; # 2 # swap後の配列をスカラーで評価して、配列長が返る
```

こういった関数の呼び出し方の間違えを防ぐには、例えば`wantarray`を利用してリスト受け取りを強制する方法があります。

```perl
use Carp qw(croak);

sub swap($x, $y) {
    croak 'Must be called in list context' unless wantarray;
    return ($y, $x);
}

my $tmp = swap('Hello', 'World');
# => Error: Must be called in list context
```

細やかな配慮になりますが、`die`ではなく`croak`を利用することで、swap関数の呼び出し元がどこか分かりやすくなります。

## 非公開の関数

通常、関数はどのパッケージからでも利用できます。例えば、パッケージ`Foo`に定義した関数`hello`は、`Foo::hello()`のようにして呼び出せます。言い換えると、関数はパッケージ`Foo`のシンボルテーブルに登録され、どこからでも参照できます。

```perl
package Foo {
  sub hello() { 'Hello!!' }
}

# パッケージFooの関数が呼び出せる。
use Foo;
Foo::hello() # 'Hello!!'
```

関数を非公開にしたい場合、`my sub hello() { }` のようにして`my`キーワードを利用します。

```perl
package Foo {
  my sub hello() { 'Hello!!' }
}

Foo::hello(); # Error!
```

関数名の先頭にアンダースコア（`_`)をつけて、紳士協定的に関数を非公開にする方法もあります。

## 関数をエクスポートする

関数を明に公開することもできます。筆者はType::Tinyを利用すると一緒についてくる[Exporter::Tiny](https://metacpan.org/pod/Exporter::Tiny)が軽量で高機能な為、よく利用しています。

例えば、次のコードは`My::Math`パッケージから関数`add`をエクスポートしています。

```perl
use v5.42;
package My::Math {
  use parent qw(Exporter::Tiny);
  our @EXPORT_OK = qw(add);

  sub add($x, $y) { $x + $y }
}
```

`use My::Math`の引数に関数名を指定すると、関数をインポートできます。

```perl
use My::Math qw(add);
add(18, 10); # 28
```

別名を利用したい場合は、`-as`を利用します。

```perl
use My::Math add => { -as => 'myadd' }
myadd(18,10); # 28
```

## SEE ALSO

- [perlsub](https://perldoc.jp/docs/perl/5.42.0/perlsub.pod)
